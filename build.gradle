plugins {
    id 'fabric-loom' version '1.9.2'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "mitaine" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

repositories {
    maven { url = "https://maven.parchmentmc.org" } // Parchment mappings

    maven { url = "https://mvn.devos.one/releases" } // Porting Lib releases
    maven { url = "https://mvn.devos.one/snapshots" } // Create and several dependencies
    maven { url = "https://maven.tterrag.com/" } // Flywheel
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" } // Forge Config API Port

    maven { // Fabric ASM for Porting Lib
        url = "https://jitpack.io/"
        content { includeGroupAndSubgroups("com.github") }
    }

    maven { url = "https://maven.shedaniel.me" } // Cloth Config, REI
    maven { url = "https://maven.blamejared.com" } // JEI

    maven { url = "https://maven.terraformersmc.com/releases" } // Mod Menu, EMI

    maven { url = "https://squiddev.cc/maven"} // CC:Tweaked
}

configurations.configureEach {
    resolutionStrategy {
        // make sure the desired version of loader is used. Sometimes old versions are pulled in transitively.
        force("net.fabricmc:fabric-loader:$project.loader_version")
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // create
    modImplementation("com.simibubi.create:create-fabric-$project.minecraft_version:$project.create_version")

    // numismatics
    modImplementation "dev.ithundxr.createnumismatics:CreateNumismatics-fabric-$project.minecraft_version:$project.numismatics_version"

    // computercraft
    modLocalRuntime("cc.tweaked:cc-tweaked-$project.minecraft_version-fabric:${project.cc_version}")

    // modmenu
    modLocalRuntime("com.terraformersmc:modmenu:$project.modmenu_version")

    // JEI
    modCompileOnly("mezz.jei:jei-$minecraft_version-fabric:$project.jei_version")
    modCompileOnly("mezz.jei:jei-$minecraft_version-common:$project.jei_version")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

import org.apache.tools.ant.taskdefs.condition.Os

if (Os.isFamily(Os.FAMILY_UNIX)) {
    rootProject.buildDir = file("/tmp/build")
}
